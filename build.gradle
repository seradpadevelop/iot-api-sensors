buildscript {
    ext {
        springBootVersion = '2.1.3.RELEASE'
        springSecurityVersion = '5.1.3.RELEASE'
        springfoxversion = '2.9.2'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.hibernate:hibernate-gradle-plugin:5.4.0.Final")
    }
}

plugins {
    id "org.sonarqube" version "2.7"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sonarqube {
    properties {
        property "sonar.projectKey", "iot-api-sensors"
        property "sonar.host.url", System.getenv('SONAR_HOST')
        property "sonar.login", System.getenv('SONAR_TOKEN')
    }
}

jacoco {
    reportsDir = file("$buildDir/jacoco")
}

test {
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}

project.version = System.getenv('PROJECT_VERSION')

bootJar {
    baseName = 'iotapisensors'
}

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {

	implementation "org.json:json:20090211"
	implementation "org.springframework:spring-jdbc"
	implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation fileTree(dir: "lib", include: "*.jar")
    implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"

    // https://projectlombok.org
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // security
	implementation "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
    implementation "org.springframework.security:spring-security-oauth2-client:${springSecurityVersion}"
    implementation "org.springframework.security:spring-security-oauth2-jose:${springSecurityVersion}"
    implementation "org.springframework.security:spring-security-oauth2-resource-server:${springSecurityVersion}"

    // GSON, RabbitMQ
    implementation "com.google.code.gson:gson:2.8.5"
    implementation "org.springframework.boot:spring-boot-starter-amqp"

    // API Documentation Dependencies
    implementation "io.springfox:springfox-swagger2:${springfoxversion}"
    implementation "io.springfox:springfox-swagger-ui:${springfoxversion}"

    // test dependencies
    testImplementation  "com.jayway.jsonpath:json-path"
    testImplementation  "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.springframework.security:spring-security-test:5.1.4.RELEASE"
}
